apply plugin: 'com.google.firebase.appdistribution'

android {
    buildToolsVersion "30.0.3"
    compileSdkVersion 33
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['../assets']
            jniLibs.srcDirs = ['libs']
        }

    }
    packagingOptions {
        exclude 'META-INF/robovm/ios/robovm.xml'
    }
    defaultConfig {
        applicationId "ru.dekabrsky.italks"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", 'ENDPOINT', "\"http://83.222.11.163/\""
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropsFile = file("keystore/keystore_config")

            if (keystorePropsFile.exists()) {
                file("keystore/keystore_config").withInputStream { keystoreProperties.load(it) }
                storeFile file("$keystoreProperties.storeFile")
                storePassword "$keystoreProperties.storePassword"
                keyAlias "$keystoreProperties.keyAlias"
                keyPassword "$keystoreProperties.keyPassword"
            } else {
                storeFile file("keystore/easy_life.keystore")
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias System.getenv('RELEASE_SIGN_KEY_ALIAS')
                keyPassword System.getenv('RELEASE_SIGN_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives {
    doFirst {
        file("libs/armeabi-v7a/").mkdirs()
        file("libs/arm64-v8a/").mkdirs()
        file("libs/x86_64/").mkdirs()
        file("libs/x86/").mkdirs()

        configurations.natives.copy().files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
            if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
            if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
            if(outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach { packageTask ->
    packageTask.dependsOn 'copyAndroidNatives'
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.mygdx.game/com.mygdx.game.AndroidLauncher'
}

dependencies {
    implementation project(':app')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'


    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    dependenciesSet(this) {
        rxJava()
        moxy()
        toothpick()
        retrofit()
        gson()
    }

    implementation libraries.cicerone

    implementation "com.github.moxy-community:moxy-android:2.2.1"
    implementation "com.github.moxy-community:moxy-androidx:2.2.1"
    implementation "com.github.moxy-community:moxy-material:2.2.1"

    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:3.10.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'

    implementation project(':scopes')
    implementation project(':basic')
    implementation project(':flows')
    implementation project(':uikit')

    implementation project(':feature:tabs')
    implementation project(':feature:chats')
    implementation project(':feature:patients')
    implementation project(':feature:events')
    implementation project(':feature:tester-settings')

    implementation project(':feature:chats-common')
    implementation project(':feature:events-common')
    implementation project(':feature:stats')
    implementation project(':feature:login')
    implementation project(':feature:game')

    implementation project(':feature:notifications:common')
}

firebaseAppDistribution {
    appId = "1:352561988518:android:b078b6b11087de02a40567"
    releaseNotes = "Test Notes"
    serviceCredentialsFile = System.getenv("GOOGLE_APPLICATION_CREDENTIALS")
    groups= "testers"
}

